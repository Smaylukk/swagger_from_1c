
Функция СформироватьJsonSwagger() Экспорт 
	ОписаниеСервиса = Новый Соответствие;
	
	Инфо = Новый Соответствие;
	Инфо.Вставить("title", ЭтотОбъект.title);
	Инфо.Вставить("summary", ЭтотОбъект.summary);
	Инфо.Вставить("description", ЭтотОбъект._description);
	Инфо.Вставить("termsOfService", ЭтотОбъект.termsOfService);
	Инфо.Вставить("version", ЭтотОбъект.version);
	
	Если ЗначениеЗаполнено(ЭтотОбъект.contactName) Или ЗначениеЗаполнено(ЭтотОбъект.contactUrl) Или ЗначениеЗаполнено(ЭтотОбъект.contactEmail) Тогда
		Контакты = Новый Соответствие;
		Контакты.Вставить("name", ЭтотОбъект.contactName);
		Контакты.Вставить("url", ЭтотОбъект.contactUrl);
		Контакты.Вставить("email", ЭтотОбъект.contactEmail);
		Инфо.Вставить("contact", Контакты);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.licanseName) Тогда
		Лицензия = Новый Соответствие;
		Лицензия.Вставить("name", ЭтотОбъект.licanseName);
		Лицензия.Вставить("url", ЭтотОбъект.licanseUrl);
		Лицензия.Вставить("identifier", ЭтотОбъект.licanseIdentifier);
		Инфо.Вставить("license", Лицензия);
	КонецЕсли;
	
	ОписаниеСервиса.Вставить("openapi", Константы.ВерсияOpenAPI.Получить());
	ОписаниеСервиса.Вставить("info", Инфо);
	
	Если ЗначениеЗаполнено(ЭтотОбъект.components) Тогда
		ОписаниеСервиса.Вставить("components", ОбщегоНазначенияSwagger.СформироватьОписаниеОбъектаSwagger(ЭтотОбъект.components));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.externalDocs) Тогда
		ОписаниеСервиса.Вставить("externalDocs", ОбщегоНазначенияSwagger.СформироватьОписаниеОбъектаSwagger(ЭтотОбъект.externalDocs));
	КонецЕсли;
	
	Если ЭтотОбъект.paths.Количество() > 0 Тогда
		ОписаниеСервиса.Вставить("paths", ОбщегоНазначенияSwagger.СформироватьСоответствиеИзТЧ(ЭтотОбъект, "paths", "path", "pathItem"));
	КонецЕсли;
	
	Если ЭтотОбъект.servers.Количество() > 0 Тогда
		ОписаниеСервиса.Вставить("servers", ОбщегоНазначенияSwagger.СформироватьМассивИзТЧ(ЭтотОбъект, "servers", "server"));
	КонецЕсли;
	
	Если ЭтотОбъект.tags.Количество() > 0 Тогда
		Массив = Новый Массив;
		Для каждого ТекСтрока Из ЭтотОбъект.tags Цикл
			Массив.Добавить(Строка(ТекСтрока.tag));
		КонецЦикла;
		ОписаниеСервиса.Вставить("tags", Массив);
	КонецЕсли;
	
	Возврат КоннекторHTTP.ОбъектВJson(ОписаниеСервиса);
КонецФункции
