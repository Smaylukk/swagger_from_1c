
&НаКлиенте
Процедура ПосмотретьСтраницу(Команда)
	#Если Клиент И Не ВебКлиент Тогда
	ТекстСтраницы = Ответы.ПолучитьТекстСтраницыДляПроверки(Объект.Ссылка);
	
	ТД = Новый ТекстовыйДокумент;
	ТД.УстановитьТекст(ТекстСтраницы);
	ИмяВремФайла = ПолучитьИмяВременногоФайла("html");
	ТД.Записать(ИмяВремФайла, КодировкаТекста.UTF8);
	
	ЗапуститьПриложение(ИмяВремФайла);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура Изображение(Команда)
	ОО = Новый ОписаниеОповещения("ПослеВыбораИзображения", ЭтотОбъект);
	П = Новый Структура;
	П.Вставить("Отбор", Новый Структура("ВидРесурса", ПредопределенноеЗначение("Перечисление.ВидыРесурса.Изображение")));
	ОткрытьФорму("Справочник.Ресурсы.ФормаВыбора", П, ЭтаФорма, , , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Функция ПослеВыбораИзображения(РезультатВыбора, ДополнительныеПараметры) экспорт
	Если РезультатВыбора <> Неопределено Тогда
		Если РежимВебКлиента Тогда
			Элементы.Шаблон.ВыделенныйТекст = УправлениеКонтентом.ПолучитьПредставлениеРесурсаНаСервере(РезультатВыбора);
		Иначе
			РедакторКлиент.УстановитьТекстМонако(Монако(), УправлениеКонтентом.ПолучитьПредставлениеРесурсаНаСервере(РезультатВыбора));
		КонецЕсли;
		
	КонецЕсли;
КонецФункции

&НаСервере
Функция ПутьВебИзображения(Изображение)
	Возврат Изображение.ПутьВеб;
КонецФункции

&НаКлиенте
Процедура ВставитьБлок(Команда)
	ОО = Новый ОписаниеОповещения("ПослеВыбораМакета", ЭтотОбъект);
	ОткрытьФорму("Справочник.МакетыБлоков.ФормаВыбора", Новый Структура, ЭтотОбъект, , , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораМакета(РезультатВыбора, ДопПараметры) Экспорт
	Если РезультатВыбора <> Неопределено Тогда
		ТекстБлока = УправлениеКонтентом.ПолучитьПредставлениеБлокаНаСервере(РезультатВыбора);
		
		Если РежимВебКлиента Тогда
			Элементы.Шаблон.ВыделенныйТекст = ТекстБлока;
		Иначе
			РедакторКлиент.УстановитьТекстМонако(Монако(), ТекстБлока);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВставитьПеременную(Команда)
	СписокПеременных = Ответы.ПолучитьСписокПеременных();
	
	ОО = Новый ОписаниеОповещения("ПослеВыбораПеременной", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОО, СписокПеременных, Элементы.ВставитьПеременную); 
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПеременной(ВыбранныйЭлемент, ДопПараметры) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Если РежимВебКлиента Тогда
			Элементы.Шаблон.ВыделенныйТекст = ВыбранныйЭлемент.Значение;
		Иначе
			РедакторКлиент.УстановитьТекстМонако(Монако(), ВыбранныйЭлемент.Значение);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСсылку(Команда)
	СписокПеременных = Ответы.ПолучитьСписокСсылок();
	
	ОО = Новый ОписаниеОповещения("ПослеВыбораПеременной", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОО, СписокПеременных, Элементы.ВставитьСсылку); 
КонецПроцедуры

&НаКлиенте
Процедура ВставитьФайл(Команда)
	ОО = Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтотОбъект);
	П = Новый Структура;
	//П.Вставить("Отбор", Новый Структура("ВидРесурса", ПредопределенноеЗначение("Перечисление.ВидыРесурса.Файл")));
	ОткрытьФорму("Справочник.Ресурсы.ФормаВыбора", П, ЭтаФорма, , , , ОО, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Функция ПослеВыбораФайла(РезультатВыбора, ДополнительныеПараметры) экспорт
	Если РезультатВыбора <> Неопределено Тогда
		Если РежимВебКлиента Тогда
			Элементы.Шаблон.ВыделенныйТекст = УправлениеКонтентом.ПолучитьПредставлениеРесурсаНаСервере(РезультатВыбора);
		Иначе
			РедакторКлиент.УстановитьТекстМонако(Монако(), УправлениеКонтентом.ПолучитьПредставлениеРесурсаНаСервере(РезультатВыбора));
		КонецЕсли;
		
	КонецЕсли;
КонецФункции

&НаСервере
Функция ПолучитьМакетБлокаНаСервере(УИД)
	Возврат Справочники.МакетыБлоков.ПолучитьСсылку(Новый УникальныйИдентификатор(УИД));
КонецФункции

&НаКлиенте
Процедура ОткрытьМакетБлока(Команда)
	Если РежимВебКлиента Тогда
		Текст = Элементы.Шаблон.ВыделенныйТекст;
	Иначе
		Текст = РедакторКлиент.ПолучитьВыделенныйТекстМонако(Монако());
	КонецЕсли;
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	Начало = СтрНайти(Текст, "{", НаправлениеПоиска.СНачала);
	Окончание = СтрНайти(Текст, "}", НаправлениеПоиска.СНачала);
	Текст = Сред(Текст, Начало, (Окончание - Начало));
	
	УидМакета = Текст;
	УидМакета = СтрЗаменить(УидМакета, "{#Блок_", "");
	УидМакета = СтрЗаменить(УидМакета, "#}", "");
	Макет = ПолучитьМакетБлокаНаСервере(УидМакета);
	Если Не Макет.Пустая() Тогда
		ПоказатьЗначение(, Макет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция Монако()
	Возврат Элементы.ТекстШаблона.Документ.defaultView;
КонецФункции

&НаКлиенте
Функция Editor()
	Возврат Элементы.ТекстШаблона.Документ.monaco.editor;
КонецФункции

&НаКлиенте
Процедура ОбработкаИнициализации(Результат, ДопПараметры) Экспорт
	ТекстШаблона = ДопПараметры.КаталогИсходников + "index.html";
	
	РедакторРаспакован = истина;
КонецПроцедуры

&НаКлиенте
Процедура ОшибкаИнициализацииРедактора(ТекстОшибки)
	ПоказатьПредупреждение(, ТекстОшибки);
КонецПроцедуры

&НаКлиенте
Процедура ШаблонДокументСформирован(Элемент)
	Если РедакторРаспакован  Тогда
		Попытка
			РедакторКлиент.ИнициализироватьРедакторMonaco(Монако(), "html");
			РедакторКлиент.ЗагрузитьТекстВРедактор(Монако(), Объект.Шаблон);
		Исключение
			ОшибкаИнициализацииРедактора("Не удалось инициализировать редактор кода - " + ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АдресМакета = ПоместитьВоВременноеХранилище(ПолучитьОбщийМакет("monaco"));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Состояние("Инициализация...");
	#Если ВебКлиент Тогда
		РежимВебКлиента = Истина;
	#иначе
		РежимВебКлиента = Ложь;
	#КонецЕсли

	Если РежимВебКлиента Тогда
		Элементы.ГруппаРедактированияТекста.ТекущаяСтраница = Элементы.ГруппаРедакторОбычный;
	Иначе
		Элементы.ГруппаРедактированияТекста.ТекущаяСтраница = Элементы.ГруппаРедакторМонако;
		РедакторКлиент.ИзвлечьИсходники(АдресМакета, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекстШаблонаПриИзменении(Элемент)
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Не ЗавершениеРаботы Тогда
		Если Не РежимВебКлиента Тогда
			Если Объект.Шаблон <> РедакторКлиент.ПолучитьТекстМонако(Монако()) Тогда
				Объект.Шаблон = РедакторКлиент.ПолучитьТекстМонако(Монако());
				ЭтаФорма.Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Не РежимВебКлиента Тогда
		ТекстМонако = РедакторКлиент.ПолучитьТекстМонако(Монако());
		Если Объект.Шаблон <> ТекстМонако Тогда
			Объект.Шаблон = ТекстМонако;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТекст(Команда)
	ОО = Новый ОписаниеОповещения("ПослеВводаТекста", ЭтотОбъект);
	ПоказатьВводСтроки(ОО, "", , , Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаТекста(Текст, ДопПараметры) Экспорт 
	Если Текст <> Неопределено Тогда
		РедакторКлиент.УстановитьТекстМонако(Монако(), Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТекст(Команда)
	Текст = РедакторКлиент.ПолучитьВыделенныйТекстМонако();
	ОчиститьСообщения();
	Сообщить(Текст);
КонецПроцедуры

&НаКлиенте
Процедура EditorСтандартныеКоманды(Команда)
	Соответствие = New Соответствие;
	Соответствие.Вставить("ClipboardCut", "editor.action.clipboardCutAction");
	Соответствие.Вставить("ClipboardCopy", "editor.action.clipboardCopyAction");
	Соответствие.Вставить("ClipboardPaste", "editor.action.clipboardPasteAction");
	Соответствие.Вставить("EditFind", "actions.find");
	Соответствие.Вставить("EditFindNext", "editor.action.nextMatchFindAction");
	Соответствие.Вставить("EditFindPrevious", "editor.action.previousMatchFindAction");
	Соответствие.Вставить("EditUndo", "undo");
	Соответствие.Вставить("EditRedo", "redo");
	Соответствие.Вставить("EditReplace", "editor.action.startFindReplaceAction");
	ИмяКоманды = СтрЗаменить(Команда.Имя, "Editor", "");
	
	Элементы.ТекстШаблона.Документ.defaultView.editor.trigger("", Соответствие[ИмяКоманды]);
КонецПроцедуры