
Функция СоздатьКаталоги(Путь) Экспорт 
	ПапкаСсылка = Справочники.Ресурсы.НайтиПоРеквизиту("ПутьВеб", СтрЗаменить(Путь, "\", "/"));
	Если Не ПапкаСсылка.Пустая() Тогда
		Возврат ПапкаСсылка;
	КонецЕсли;
	
	Папки = СтрРазделить(Путь, "\", Ложь);
	
	Родитель = Неопределено;
	Для каждого ТекПапка Из Папки Цикл
		ПапкаСсылка = СоздатьКаталогРесурса(ТекПапка, Родитель);
		
		Родитель = ПапкаСсылка;
	КонецЦикла; 
	
	Возврат ПапкаСсылка;
КонецФункции

Функция СоздатьКаталогРесурса(Имя, Родитель = Неопределено)
	Путь = "";
	Если Родитель <> Неопределено Тогда
		Путь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "ПутьВеб");
	КонецЕсли;
	Путь = Путь + "\" + Имя;
	
	ПапкаСсылка = Справочники.Ресурсы.НайтиПоРеквизиту("ПутьВеб", СтрЗаменить(Путь, "\", "/"));
	Если Не ПапкаСсылка.Пустая() Тогда
		Возврат ПапкаСсылка;
	КонецЕсли;
	
	ПапкаОбъект = Справочники.Ресурсы.СоздатьГруппу();
	ПапкаОбъект.Наименование = Имя;
	ПапкаОбъект.Родитель = Родитель;
	ПапкаОбъект.Записать();
	
	Возврат ПапкаОбъект.Ссылка;
КонецФункции

Функция СоздатьРесурс(АдресФайла, Путь, ИмяФайла, Расширение) экспорт
	Результат = Неопределено;
	
	ВидРесурса = УправлениеКонтентом.ОпределитьРесурсПоРасширению(Расширение);
	
	ПутьКРодителю = СтрЗаменить(Путь, "\" + ИмяФайла, "");
	Родитель = Справочники.Ресурсы.НайтиПоРеквизиту("ПутьВеб", СтрЗаменить(ПутьКРодителю, "\", "/"));
	
	РесурсСсылка = Справочники.Ресурсы.НайтиПоРеквизиту("ПутьВеб", СтрЗаменить(Путь, "\", "/"));
	Если РесурсСсылка.Пустая() Тогда
		НовыйРесурс = Справочники.Ресурсы.СоздатьЭлемент();
	Иначе
		НовыйРесурс = РесурсСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	НовыйРесурс.Родитель = Родитель;
	НовыйРесурс.Наименование = ИмяФайла;
	НовыйРесурс.ВидРесурса = ВидРесурса;
	Если ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
		НовыйРесурс.Файл = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
	КонецЕсли;
	
	Если ВидРесурса = ПредопределенноеЗначение("Перечисление.ВидыРесурса.Стиль") Тогда
		НовыйРесурс.Текст = ИзвлечьТекст(АдресФайла);
		НовыйРесурс.ТекстовыйФайл = Истина;
	КонецЕсли;
	
	НовыйРесурс.Записать();
	
	
	Возврат Результат;
КонецФункции

Функция ИзвлечьТекст(АдресФайла)
	Текст = "";
	Если ЭтоАдресВременногоХранилища(АдресФайла) Тогда 
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
		ИмяВремФайла = ПолучитьИмяВременногоФайла("tmp");
		ДвоичныеДанные.Записать(ИмяВремФайла);
		
		ТД = Новый ТекстовыйДокумент;
		ТД.Прочитать(ИмяВремФайла, КодировкаТекста.UTF8);
		Текст = ТД.ПолучитьТекст();
		
		УдалитьФайлы(ИмяВремФайла);
	КонецЕсли;
	Возврат Текст;
КонецФункции
